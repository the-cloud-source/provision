#!ipxe

# Some menu defaults
set menu-timeout 600000
set submenu-timeout ${menu-timeout}
isset ${menu-default} || set menu-default exit

cpuid --ext 29 && set arch x86_64 || set arch i386


:start
menu iPXE [IP: ${netX/ip} on ${netX/chip} | ${arch}]
item --gap --             ------------------------- Operating systems ------------------------------
item --key 5 centos5      Install Centos 5
item --key 6 centos6      Install Centos 6
item --key 7 centos7      Install Centos 7
item --key 8 centos8      Install Centos 8
item --key f fedora       Install Fedora 35
item --gap --             ------------------------- Rescue -------------------------------
item rescue6              Rescue Centos 6
item rescue7              Rescue Centos 7
item rescue8              Rescue Centos 8
item rescuef              Rescue Fedora
item --gap --             ------------------------- Advanced options -------------------------------
item --key c config       Configure settings
item shell                Drop to iPXE shell
item reboot               Reboot computer
item
item --key x exit         Exit iPXE and continue BIOS boot
choose --timeout ${menu-timeout} --default ${menu-default} selected || goto cancel
set menu-timeout 0
goto ${selected}

:cancel
echo You cancelled the menu, dropping you to a shell

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

:failed
echo Booting failed, dropping to shell
goto shell

:reboot
reboot

:exit
exit

:config
config
goto start

:back
set submenu-timeout 0
clear submenu-default
goto start

:centos5
chain ${boot-url}/centos5/centos5.ipxe ||
goto start

:centos6
chain ${boot-url}/centos6/centos6.ipxe ||
goto start

:rescue6
chain ${boot-url}/centos6/rescue6.ipxe ||
goto start

:centos7
chain ${boot-url}/centos7/centos7.ipxe ||
goto start

:rescue7
chain ${boot-url}/centos7/rescue7.ipxe ||
goto start

:centos8
chain ${boot-url}/centos8/centos8.ipxe ||
goto start

:rescue8
chain ${boot-url}/centos8/rescue8.ipxe ||
goto start

:fedora
chain ${boot-url}/fedora/fedora.ipxe ||
goto start

:rescuef
chain ${boot-url}/fedora/rescue.ipxe ||
goto start

